# Maintainer: My name <myemail@domain.me>

_pkgname=yubikey-manager-qt
_pkgdesktop="ykman-gui"
_pkgicon="ykman"

pkgname="${_pkgname}"-appimage
_basepkgver=1.2.3
pkgver=${_basepkgver}b
pkgrel=2
pkgdesc="Cross-platform application for configuring any YubiKey over all USB transports (AppImage)"
arch=('x86_64')
url="https://github.com/Yubico/yubikey-manager-qt"
license=('BSD')
depends=('ccid')
options=(!strip)
_appimage="${pkgname}-${pkgver}.AppImage"
source_x86_64=("${_appimage}::https://developers.yubico.com/yubikey-manager-qt/Releases/yubikey-manager-qt-${pkgver}-linux.AppImage"
               "https://raw.githubusercontent.com/Yubico/yubikey-manager-qt/yubikey-manager-qt-${_basepkgver}/COPYING"
              )
noextract=("${_appimage}")
sha256sums_x86_64=('94d404b05921569d3287835a331e7a08a64353a81c300a5d4caa58a0ce6b689c'
                   'SKIP')

prepare() {
    chmod +x "${_appimage}"
    ./"${_appimage}" --appimage-extract
}

build() {
    # Adjust .desktop so it will work outside of AppImage container
    sed -i -E "s|Exec=AppRun|Exec=env DESKTOPINTEGRATION=false /usr/bin/${_pkgname}|"\
        "squashfs-root/${_pkgdesktop}.desktop"
    # Fix permissions; .AppImage permissions are 700 for all directories
    chmod -R a-x+rX squashfs-root/usr
}

package() {
    # AppImage
    install -Dm755 "${srcdir}/${_appimage}" "${pkgdir}/opt/${pkgname}/${pkgname}.AppImage"
    install -Dm644 "${srcdir}/COPYING" "${pkgdir}/opt/${pkgname}/COPYING"

    # Desktop file
    install -Dm644 "${srcdir}/squashfs-root/${_pkgdesktop}.desktop"\
            "${pkgdir}/usr/share/applications/${_pkgdesktop}.desktop"

    # Icon file
    install -Dm644 "${srcdir}/squashfs-root/${_pkgicon}.png"\
            "${pkgdir}/usr/share/pixmaps/${_pkgicon}.png"

    # Symlink executable
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/opt/${pkgname}/${pkgname}.AppImage" "${pkgdir}/usr/bin/${_pkgdesktop}" # base desktop file does not include AppRun, is calls ykman-gui
}
