# Maintainer: My name <myemail@domain.me>

_pkgname=yubioath-desktop
_pkgnamespace="com.yubico.yubioath"

pkgname="${_pkgname}"-appimage
pkgver=5.0.5
pkgrel=1
pkgdesc="Yubico Authenticator for Desktop (AppImage)"
arch=('x86_64')
url="https://github.com/Yubico/yubioath-desktop"
license=('BSD')
depends=('ccid')
options=(!strip)
_appimage="${pkgname}-${pkgver}.AppImage"
source_x86_64=("${_appimage}::https://developers.yubico.com/yubioath-desktop/Releases/yubioath-desktop-${pkgver}-linux.AppImage"
               "https://raw.githubusercontent.com/Yubico/yubioath-desktop/yubioath-desktop-${pkgver}/COPYING"
              )
noextract=("${_appimage}")
sha256sums_x86_64=('9465e4bc766fee6f2b77b4020cdc3a8c29e19938bf54f85279112b735b6b8eb7'
                   'SKIP')

prepare() {
    chmod +x "${_appimage}"
    ./"${_appimage}" --appimage-extract
}

build() {
    # Adjust .desktop so it will work outside of AppImage container
    sed -i -E "s|Exec=AppRun|Exec=env DESKTOPINTEGRATION=false /usr/bin/${_pkgname}|"\
        "squashfs-root/${_pkgnamespace}.desktop"
    # Fix permissions; .AppImage permissions are 700 for all directories
    chmod -R a-x+rX squashfs-root/usr
}

package() {
    # AppImage
    install -Dm755 "${srcdir}/${_appimage}" "${pkgdir}/opt/${pkgname}/${pkgname}.AppImage"
    install -Dm644 "${srcdir}/COPYING" "${pkgdir}/opt/${pkgname}/COPYING"

    # Desktop file
    install -Dm644 "${srcdir}/squashfs-root/${_pkgnamespace}.desktop"\
            "${pkgdir}/usr/share/applications/${_pkgnamespace}.desktop"

    # Icon file
    install -Dm644 "${srcdir}/squashfs-root/${_pkgnamespace}.svg"\
            "${pkgdir}/usr/share/pixmaps/${_pkgnamespace}.svg"

    # Symlink executable
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "/opt/${pkgname}/${pkgname}.AppImage" "${pkgdir}/usr/bin/${_pkgname}"
}
